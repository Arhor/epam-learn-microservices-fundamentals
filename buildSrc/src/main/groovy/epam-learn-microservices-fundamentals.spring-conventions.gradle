plugins {
    id "org.springframework.boot"
    id "io.spring.dependency-management"
    id "org.jetbrains.kotlin.plugin.spring"
}

dependencies {
    kapt "org.springframework:spring-context-indexer"
    kapt "org.springframework.boot:spring-boot-configuration-processor"

    developmentOnly "org.springframework.boot:spring-boot-devtools"

    implementation "com.fasterxml.jackson.module:jackson-module-kotlin"

    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-web"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.mockito.kotlin:mockito-kotlin:${property('app.version.mockitoKotlin')}"
}

configurations {
    testImplementation {
        exclude module: "junit-vintage-engine"
    }
}

task contractTest(type: Test) {
    group = "verification"
    useJUnitPlatform {
        includeTags "contract"
    }
    shouldRunAfter test
}

task integrationTest(type: Test) {
    group = "verification"
    useJUnitPlatform {
        includeTags "integration"
    }
    shouldRunAfter contractTest
}

test {
    useJUnitPlatform {
        excludeTags "contract", "integration"
    }
}

check {
    dependsOn = [
        test,
        contractTest,
        integrationTest,
    ]
}
